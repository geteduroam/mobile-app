import app.eduroam.shared.Config
import app.eduroam.shared.Libs

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.appdistribution'
    id 'com.google.gms.google-services'
}

def buildNumberFile = new File("${buildDir}/build_number.txt")
def versionNameFile = new File("version_name.txt")
def appName = versionNameFile.exists() ? versionNameFile.text.trim() as String : ""

android {
    compileSdk = Config.compileSdk
    defaultConfig {
        applicationId = "app.eduroam.geteduroam"
        minSdk = Config.minSdk
        targetSdk = Config.targetSdk
        versionName appName
        versionCode buildNumberFile.exists() ? buildNumberFile.text.trim() as Integer : 1
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    packagingOptions {
        resources.excludes.add("META-INF/*.kotlin_module")
    }

    buildTypes {
        debug {
            zipAlignEnabled false
            minifyEnabled false
            ext.enableCrashlytics = false
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            firebaseAppDistribution {
                artifactType "APK"
                artifactPath "${buildDir}/outputs/apk/debug/app-debug.apk"
                releaseNotesFile "${buildDir}/release_notes.txt"
                testersFile "android/android_alpha_testers.txt"
            }
        }

        release {
            minifyEnabled false
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    buildFeatures {
        compose = true

        // Disable unused AGP features
        buildConfig false
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    kotlinOptions {
        freeCompilerArgs = freeCompilerArgs + "-Xopt-in=kotlin.RequiresOptIn"
    }

    composeOptions {
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.kotlinCompilerExtensionVersion
    }

    packagingOptions {
        // Multiple dependency bring these files in. Exclude them to enable
        // our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation project(":shared:core")
    implementation Libs.Kotlin.stdlibJdk
    implementation Libs.Ktor.clientSerializationJvm

    implementation Libs.Kotlin.Coroutines.android
    implementation Libs.AndroidX.Activity.activityCompose
    implementation Libs.AndroidX.appcompat
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.runtimeLivedata
    implementation Libs.AndroidX.Compose.foundation
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.Material3.designMaterial3
    implementation Libs.AndroidX.Compose.materialIconsExt
    implementation Libs.AndroidX.Compose.layout
    implementation Libs.AndroidX.Compose.animation
    implementation Libs.AndroidX.Compose.toolingPreview
    implementation Libs.AndroidX.Lifecycle.viewModelCompose
    implementation Libs.AndroidX.splashCore

    implementation Libs.Accompanist.pager
    implementation Libs.Accompanist.pagerIndicators
    implementation Libs.Coil.coilCompose
    implementation Libs.AndroidX.Compose.Navigation.nav

    implementation platform(Libs.Firebase.billOfMaterials)
    implementation Libs.Firebase.analytics
    implementation Libs.Firebase.crashlytics
    implementation Libs.Material.design

    debugImplementation Libs.AndroidX.Compose.tooling

    implementation Libs.AndroidX.Lifecycle.viewModelKtx
    implementation Libs.AndroidX.Lifecycle.runtimeKtx
    implementation Libs.Koin.android

    debugImplementation Libs.AndroidX.Compose.uiTestManifest
}